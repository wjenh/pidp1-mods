/ Read a line of characters into the memory address passed in the IO register.
/ One character is placed in each word.
/ The line is terminated by the nl character, flxnl, 077.
/ The memory area is not bounds checked!
/ The current DCS2 channel is used.
/ If an error occurs, the IO register will have the DCS2 error code, else 0.
/ The AC register is preserved.
/ Call via jda getlin.

#includeif <1Dinstructions.mh>
#includeif <DCS/dcs2defs.mh>
#includeif <DCS/getch.mh>

getlin, 0
        dac glnrtn
        dio glnmem
glnlp,  jda getch
        lai
        sad (flxerr
        jmp glnbad
        dio i glnmem
        law flxnl
        sad i glnmem
        jmp glndon
        idx glnmem
        jmp glnlp
glndon, lac getlin
        cli
        jmp i glnrtn
glnbad, iot rle
        lac getlin
        jmp i glnrtn
glnrtn, 0
glnmem, 0
