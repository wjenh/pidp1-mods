/ Read a line of ascii characters into the memory address passed in the IO register.
/ One character is placed in each word.
/ The line is terminated by the null character, 0, and the line terminatior (nl or cr) dropped.
/ The memory area is not bounds checked!
/ The current DCS2 channel is used.
/ If an error occurs, the IO register will have the DCS2 error code, else 0.
/ The AC register is preserved.
/ Call via jda getaln.

#includeif <1Dinstructions.mh>
#includeif <DCS/dcs2defs.mh>
#includeif <DCS/getach.mh>

getaln, 0
        dac glartn
        dio glamem
glalp,  jda getach
        lai
        spa
        jmp glabad
        dio i glamem
        sad ('\r'
        jmp gladon
        sad ('\n'
        jmp gladon
        idx glamem
        jmp glalp
gladon, lac getaln
        cli
/ overwrite the cr or nl with 0
        dzm i glamem
        jmp i glartn
glabad, iot rle
        lac getaln
        jmp i glartn
glartn, 0
glamem, 0
